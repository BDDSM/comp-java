
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступныеJRE.ЗагрузитьЗначения(КомпонентыJava.ДоступныеJRE());
	
	Если Параметры.Свойство("JAVA_HOME") Тогда
		Если ДоступныеJRE.НайтиПоЗначению(Параметры.JAVA_HOME) = Неопределено Тогда
			JAVA_HOME = Параметры.JAVA_HOME;
			Элементы.СтраницыВыборJRE.ТекущаяСтраница = Элементы.СтраницаПроизвольный;
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура JAVA_HOMEНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	

	Оповещение = Новый ОписаниеОповещения("ВыборКаталогаЗавершениеВвода", ЭтотОбъект);
		
	ОткрытьФорму(
		"Справочник.КомпонентыJava.Форма.ВыборКаталога",
		Новый Структура("ВыбранныйКаталог", Элемент.ТекстРедактирования),
		,
		,
		,
		,
		Оповещение);
		
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьJRE(Команда)
	
	ТекущаяСтраница = Элементы.СтраницыВыборJRE.ТекущаяСтраница;
	
	Если ТекущаяСтраница = Элементы.СтраницаДоступныеJRE Тогда	
		ТекущиеДанные = Элементы.ДоступныеJRE.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда	
			ОповеститьОВыборе(ТекущиеДанные.Значение);
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаПроизвольный Тогда
		ОповеститьОВыборе(JAVA_HOME);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

Процедура ВыборКаталогаЗавершениеВвода(Результат, ДополнительныеПараметры) Экспорт
	
	JAVA_HOME = Результат;
	
КонецПроцедуры

#КонецОбласти

