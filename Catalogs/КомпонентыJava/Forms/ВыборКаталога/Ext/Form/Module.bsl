
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 ИЛИ
		СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда		
		Корень = "C:/";
	Иначе
		Корень = "/";
	КонецЕсли;
	
	ЗаполнитьСписокФайлов(ЭтаФорма, Файлы, Корень);
		
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ФайлыПередРазворачиванием(Элемент, Строка, Отказ)
	
	ВыбранныйУзел = Файлы.НайтиПоИдентификатору(Строка);
	Если ВыбранныйУзел <> Неопределено Тогда
		ЗаполнитьСписокФайлов(ЭтаФорма, Строка, ВыбранныйУзел.ПолноеИмя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьКаталог(Команда)

	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.ПолноеИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте     
Процедура НовыйКаталог(Команда)

	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите каталог для создания нового'"));
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗавершениеВводаИмениНовогоКаталога", 
		ЭтаФорма,
		Новый Структура("ВыбранныйКаталог, ТекущаяСтрока", ТекущиеДанные.ПолноеИмя, Элементы.Файлы.ТекущаяСтрока));	
		
	ПоказатьВводСтроки(Оповещение, "Новый", НСтр("ru = 'Введите имя нового каталога'"), 255, Ложь);
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершениеВводаИмениНовогоКаталога(Результат, ДополнительныеПараметры) Экспорт
	
	Результат = СоздатьКаталогНаСервере(
		ДополнительныеПараметры.ВыбранныйКаталог + "/" + Результат);
	
	Если Результат Тогда
		// Обновление узла дерева
		ВыбранныйУзел = Файлы.НайтиПоИдентификатору(ДополнительныеПараметры.ТекущаяСтрока);
		ЗаполнитьСписокФайлов(
			ЭтаФорма, 
			ВыбранныйУзел, 
			ВыбранныйУзел.ПолноеИмя);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Ошибка создания нового каталога'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьКаталогНаСервере(Имя)
	
	Результат = Ложь;
	
	СоздатьКаталог(Имя);		
	СозданныйКаталог = Новый Файл(Имя);		
	Результат = СозданныйКаталог.Существует() И СозданныйКаталог.ПолноеИмя = Имя;
		
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокФайлов(Путь)
	
	Результат = Новый Массив();
		
	НайденныеФайлы = НайтиФайлы(Путь, "*", Ложь);			
	Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
		Если НайденныйФайл.ЭтоКаталог() И НЕ НайденныйФайл.ПолучитьНевидимость() Тогда
			Результат.Добавить(				
				Новый Структура("Имя, ПолноеИмя", НайденныйФайл.Имя, НайденныйФайл.ПолноеИмя));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокФайлов(ЭтаФорма, ТекущийУзел, Путь, Знач Глубина = 0)
		
	Если Глубина > 1 Тогда
		Возврат;
	КонецЕсли;
	
	НайденныеФайлы = СписокФайлов(Путь);
	Если ТипЗнч(ТекущийУзел) = Тип("Число") Тогда
		ТекущийУзел = ЭтаФорма.Файлы.НайтиПоИдентификатору(ТекущийУзел);
	КонецЕсли;
		
	ЭлементыУзла = ТекущийУзел.ПолучитьЭлементы();	
	Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
		ПодчиненныйЭлемент = Неопределено;
		Для Каждого ПодчиненныйЭлементУзла Из ЭлементыУзла Цикл
			Если ПодчиненныйЭлементУзла.ПолноеИмя = НайденныйФайл.ПолноеИмя Тогда
				ПодчиненныйЭлемент = ПодчиненныйЭлементУзла;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ПодчиненныйЭлемент = Неопределено Тогда				
			ПодчиненныйЭлемент = ЭлементыУзла.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПодчиненныйЭлемент, НайденныйФайл);
		ЗаполнитьСписокФайлов(ЭтаФорма, ПодчиненныйЭлемент, НайденныйФайл.ПолноеИмя, Глубина + 1);
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти