
Перем ПроксиКомпоненты;

///////////////////////////////////////////////////////////////////////////////
// УПРАВЛЕНИЕ СЕССИЕЙ

#Область УправлениеСессией

Функция ОткрытьСессию() Экспорт
	
	ПроксиКомпоненты = ПроксиКомпоненты();
	
	Соединение = ПроксиКомпоненты.ФабрикаXDTO.Создать("http://ldap.ak.ru/", "connection");
	Соединение.host = Хост;
	Соединение.port = Порт;
	Соединение.login = Пользователь;
	Соединение.password = Пароль;
	Соединение.domain = Домен;
		
	uuid = ПроксиКомпоненты.connect(Соединение, ВидLDAP);
	
КонецФункции

Функция ЗакрытьСессию() Экспорт
	
	ПроксиКомпоненты().disconnect(uuid);		
	uuid = Неопределено;
	
КонецФункции

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ПРИКЛАДНЫЕ МЕТОДЫ

#Область ПрикладныеМетоды

Функция НайтиВсе(Домен) Экспорт
	
	Возврат ПоискПоФильтру(Домен, "(objectClass=*)");
		
КонецФункции

Функция ПоискПоФильтру(Домен, Фильтр) Экспорт
	
	Результат = Новый Массив();
	
	ДанныеLDAP = ПроксиКомпоненты().searchByFilter(uuid, Домен, Фильтр);
	Для Каждого Entry Из ДанныеLDAP.entries.entry Цикл
		Запись = Новый Структура("DN, Атрибуты", Entry.dn, Новый Соответствие());				
		Для Каждого Attribute Из Entry.attributes.attribute Цикл
			Запись.Атрибуты.Вставить(Attribute.name, Attribute.value);
		КонецЦикла;
		
		Результат.Добавить(Запись);		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

Функция ПроксиКомпоненты()
	
	ОписаниеОшибки = Неопределено;
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = КомпонентыJavaПовтИсп.ПроксиКомпоненты(
			Справочники.КомпонентыJava.ldapclient, ОписаниеОшибки);
	КонецЕсли;
		
	Если ПроксиКомпоненты = Неопределено Тогда
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			ОписаниеОшибки = НСтр("ru = 'Java-компонента <LDAP-клиент> не найдена'");
		КонецЕсли;
		
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
			
	Возврат ПроксиКомпоненты;
	
КонецФункции

#КонецОбласти