
Перем ПроксиКомпоненты;

///////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ SFTP

#Область КомандыSFTP

Функция connect(ОписаниеОшибки = Неопределено) Экспорт
	
	ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	Если ПроксиКомпоненты <> Неопределено Тогда
		Соединение = ПроксиКомпоненты.ФабрикаXDTO.Создать("http://model.sftp.ak.ru/", "connection");
		Соединение.host = Хост;
		Соединение.port = Порт;
		Соединение.user = Пользователь;
		Соединение.password = Пароль;
		
		Ответ = ПроксиКомпоненты.connect(Соединение);
		Если Ответ.error = Истина Тогда
			ОписаниеОшибки = Ответ.description;	
		Иначе
			uuid = Ответ.object;	
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция disconnect(ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
		
	Если ПроксиКомпоненты <> Неопределено Тогда		
		Ответ = ПроксиКомпоненты.disconnect(uuid);		
		uuid = Неопределено;		
	КонецЕсли;
	
КонецФункции

Функция put(Источник, Назначение, ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <>  Неопределено Тогда
		Ответ = ПроксиКомпоненты.put(uuid, Источник, Назначение);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция get(Источник, Назначение, ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
		
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.get(uuid, Источник, Назначение);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция rename(Источник, НовоеИмя, ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.rename(uuid, Источник, НовоеИмя);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;		
	КонецЕсли;
	
КонецФункции

Функция rm(ИмяФайла, ЭтоКаталог = Ложь, ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.rm(uuid, ИмяФайла, ЭтоКаталог);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция mkdir(ИмяКаталога, ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.mkdir(uuid, ИмяКаталога);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;		
	КонецЕсли;
	
КонецФункции

Функция pwd(ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.pwd(uuid);
		Если Ответ.error = Истина Тогда
			ОписаниеОшибки = Ответ.description;	
		Иначе
			Результат = Ответ.object;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция cd(ИмяКаталога, ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.cd(uuid, ИмяКаталога);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;
	КонецЕсли;
		
КонецФункции

Функция ls(ИмяКаталога, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ЭтоКаталог", Новый ОписаниеТипов("Булево"));
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.ls(uuid, ИмяКаталога);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		Иначе
			Для Каждого ИнформацияОФайле Из Ответ.InfoFiles.InfoFile Цикл
				НоваяСтрока = Результат.Добавить();
				НоваяСтрока.Имя = ИнформацияОФайле.name;
				НоваяСтрока.ЭтоКаталог = ИнформацияОФайле.isDir;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Результат.Сортировать("ЭтоКаталог УБЫВ, Имя ВОЗР");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

Функция ПроксиКомпоненты(ОписаниеОшибки = Неопределено)
	
	Прокси = КомпонентыJavaПовтИсп.ПроксиКомпоненты(
		Справочники.КомпонентыJava.sftpclient, ОписаниеОшибки);
		
	Возврат Прокси;
	
КонецФункции

#КонецОбласти