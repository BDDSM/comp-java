
Перем ПроксиКомпоненты;


///////////////////////////////////////////////////////////////////////////////
// ОЧЕРЕДИ

#Область Очереди

// Отправка сообщения 
//
// Параметры:
//  ИмяОчереди		 - Строка - Имя очереди в брокере
//  ТекстСообщения	 - Строка - Текст сообщения
// 
// Возвращаемое значение:
//   - 
//
Функция Отправить(ИмяОчереди, ТекстСообщения, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Прокси = Прокси(ОписаниеОшибки);
	Если Прокси <> Неопределено Тогда
		Результат = Прокси.produce(ИмяОчереди, Адрес, Логин, Пароль, ТекстСообщения);
	Иначе
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			ОписаниеОшибки = НСтр("ru = 'JMS-клиент не доступен'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получение сообщения из очереди (FIFO)
//
// Параметры:
//  ИмяОчереди	 - Строка - Имя очереди
// 
// Возвращаемое значение:
//   - 
//
Функция ПолучитьСообщение(ИмяОчереди, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Неопределено;
	Прокси = Прокси(ОписаниеОшибки);
	Если Прокси <> Неопределено Тогда
		Результат = Прокси.consume(ИмяОчереди, Адрес, Логин, Пароль);	
	Иначе
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			ОписаниеОшибки = НСтр("ru = 'JMS-клиент не доступен'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получение сообщений из очереди
//
// Параметры:
//  ИмяОчереди		 - Строка - Имя очереди
//  ОписаниеОшибки	 - Строка, Неопределено - Описание ошибки
// 
// Возвращаемое значение:
//  Массив - Массив сообщений
//
Функция ПолучитьСообщения(ИмяОчереди, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Новый Массив();
	
	Пока Истина Цикл
		Сообщение = ПолучитьСообщение(ИмяОчереди, ОписаниеОшибки);		
		Если Сообщение = Неопределено Тогда
			Прервать;
		Иначе
			Если НЕ ЗначениеЗаполнено(Сообщение.messageId) Тогда
				Прервать;
			КонецЕсли;			
		КонецЕсли;
				
		Результат.Добавить(
			Новый Структура("Код, ТекстСообщения", Сообщение.messageId, Сообщение.textMessage));		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Проверка доступности компоненты
// 
// Возвращаемое значение:
//  Булево - 
//
Функция ПроверитьДоступность(ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Ложь;
	
	Прокси = Прокси(ОписаниеОшибки);
	Если Прокси <> Неопределено Тогда
		ОтветXDTO = Прокси.ping(Адрес, Логин, Пароль);
		Результат = Не ОтветXDTO.error;
		ОписаниеОшибки = ОтветXDTO.description;
	Иначе
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			ОписаниеОшибки = НСтр("ru = 'JMS-клиент не доступен'");
		КонецЕсли;		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

Функция Прокси(ОписаниеОшибки = Неопределено)
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = КомпонентыJavaПовтИсп.ПроксиКомпоненты(
			Справочники.КомпонентыJava.jmsclient, ОписаниеОшибки);			
	КонецЕсли;
			
	Возврат ПроксиКомпоненты;
	
КонецФункции

#КонецОбласти
