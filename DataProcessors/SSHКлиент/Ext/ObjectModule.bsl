
Перем ПроксиКомпоненты;

///////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ SSH КЛИЕНТА

#Область УправлениеСессией

Функция ОткрытьСессию(ОписаниеОшибки = Неопределено) Экспорт
	
	ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	Если ПроксиКомпоненты <> Неопределено Тогда
		Соединение = ПроксиКомпоненты.ФабрикаXDTO.Создать("http://model.sshclient.ak.ru/", "connection");
		Соединение.host = Хост;
		Соединение.port = Порт;
		Соединение.user = Пользователь;
		Соединение.password = Пароль;
		
		Ответ = ПроксиКомпоненты.connect(Соединение);
		Если Ответ.error = Истина Тогда
			ОписаниеОшибки = Ответ.description;	
		Иначе
			uuid = Ответ.object;	
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ЗакрытьСессию(ОписаниеОшибки = Неопределено) Экспорт
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
		
	Если ПроксиКомпоненты <> Неопределено Тогда		
		Ответ = ПроксиКомпоненты.disconnect(uuid);		
		uuid = Неопределено;		
	КонецЕсли;
	
КонецФункции

#КонецОбласти


#Область Туннели

Функция ДобавитьЛокальныйПорт(Локальный, лХост, Удаленный, ОписаниеОшибки = Неопределено) Экспорт
	
	Возврат ДобавитьПорт(Истина, Локальный, лХост, Удаленный, ОписаниеОшибки);
	
КонецФункции

Функция ДобавитьУдаленныйПорт(Удаленный, лХост, Локальный, ОписаниеОшибки = Неопределено) Экспорт
	
	Возврат ДобавитьПорт(Ложь, Локальный, лХост, Удаленный, ОписаниеОшибки);
		
КонецФункции

Функция ДобавитьПорт(ЭтоЛокальный, Локальный, лХост, Удаленный, ОписаниеОшибки = Неопределено)
	
	Ответ = Неопределено;
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <>  Неопределено Тогда
		ТуннельXDTO = ПроксиКомпоненты.ФабрикаXDTO.Создать("http://model.sshclient.ak.ru/", "tunnel");
		ТуннельXDTO.host       = лХост;
		ТуннельXDTO.localPort  = Локальный;
		ТуннельXDTO.remotePort = Удаленный;				
		
		Ответ = ПроксиКомпоненты.setPortForwarding(uuid, ЭтоЛокальный, ТуннельXDTO);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ответ;
		
КонецФункции

Функция УдалитьПорт(ЭтоЛокальный, Порт, ОписаниеОшибки = Неопределено) Экспорт
	
	Ответ = Неопределено;
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	КонецЕсли;
	
	Если ПроксиКомпоненты <>  Неопределено Тогда
		Ответ = ПроксиКомпоненты.delPortForwarding(uuid, ЭтоЛокальный, Порт);
		Если Ответ.Error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ответ;
		
КонецФункции

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

Функция ПроксиКомпоненты(ОписаниеОшибки = Неопределено)
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = КомпонентыJavaПовтИсп.ПроксиКомпоненты(
			Справочники.КомпонентыJava.sshclient, ОписаниеОшибки);
	КонецЕсли;	
	
	Возврат ПроксиКомпоненты;
	
КонецФункции

#КонецОбласти